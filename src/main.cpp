/*******************************************************************************
 * Title                 :   Main
 * Filename              :   main.cpp
 * Author                :   Simone Gasparella
 * Origin Date           :   19/01/2022
 * Compiler              :   xtensa32 2.50200.97
 * Target                :   ESP32WROOMSE
 * Notes                 :   None
 *
 *******************************************************************************/
/****************************************************************************
 * Doxygen C Template
 *
 *
 *****************************************************************************/
/*************** SOURCE REVISION LOG *****************************************
 *
 *    Date    Version   Author         Description
 *  19/01/22   1.0.0   Simone Gasparella   Initial Release.
 *
 *******************************************************************************/
/** @file : main.cpp
 *  @brief This is the main file of the example. Connection with aws iot with ethernet shield ENCj2860 comm with esp32 via SPI
 */

#include <Arduino.h>
#include <SPI.h>
#include <Ethernet.h>
#include "Ethernet_pinout_setting.h"
#include <PubSubClient.h>
#include <SSL_Utility/EthernetWebServer_SSL.h>
#include "AWS_security.h"

void MQTTPublish(const char *topic, char *payload);
void updateThing();
void callback(char *topic, byte *payload, unsigned int length);

SPIClass ethernetSPI(ETH_SPI_BUS);
EthernetClient ethClient;
EthernetSSLClient ethClientSSL(ethClient, TAs, (size_t)TAs_NUM, 1);
PubSubClient mqtt_istance(mqttServer, 8883, callback, ethClientSSL);
SSLClientParameters mTLS = SSLClientParameters::fromPEM(my_cert, sizeof my_cert, my_key, sizeof my_key);

/**
 * @brief  Function to execute the callback from mqtt message
 * @param  topic
 * @param  payload
 * @param  length
 * @retval None
 */
void callback(char *topic, byte *payload, unsigned int length)
{
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");

  for (unsigned int i = 0; i < length; i++)
  {
    Serial.print((char)payload[i]);
  }

  Serial.println();
}
/**
 * @brief  Function to execute the reconnection if it was not connected
 * @param  None
 * @retval None
 */
void reconnect(void)
{
  while (!mqtt_istance.connected())
  {
    Serial.print("Attempting MQTT connection...");

    if (mqtt_istance.connect("arduinoClient"))
    {
      Serial.println("connected");

      for (int i = 0; i < 5; i++)
      {
        Serial.println(subscribeTopic[i]);
        mqtt_istance.subscribe(subscribeTopic[i]);
      }

      Serial.println("Started updateThing ");
      updateThing();
      Serial.println("Done updateThing ");
    }
    else
    {
      Serial.print("failed, rc=");
      Serial.print(mqtt_istance.state());
      Serial.println(" try again in 5 seconds");
      delay(5000);
    }
  }
}
/**
 * @brief  Function to call the publish method after copy the payload
 * @param  None
 * @retval None
 */
void updateThing(void)
{
  strcpy(publishPayload, "{\"state\": {\"reported\": {\"powerState\":\"ON\"}}}");
  MQTTPublish(publishShadowUpdate, publishPayload);
}
/**
 * @brief  Function to call the publish method from the lib and print topic info
 * @param  topic
 * @param  payload
 * @retval None
 */
void MQTTPublish(const char *topic, char *payload)
{
  mqtt_istance.publish(topic, payload);
  Serial.print("Published [");
  Serial.print(topic);
  Serial.print("] ");
  Serial.println(payload);
}
/**
 * @brief  Function to execute the setup generated by the arduino framework
 * @param  None
 * @retval None
 */
void setup()
{
  /*authentication on aws*/
  ethClientSSL.setMutualAuthParams(mTLS);

  Ethernet.init(ETH_CS, &ethernetSPI, ETH_SCLK, ETH_MISO, ETH_MOSI);
  Serial.begin(115200);
  while (!Serial)
  {
    ; // wait for serial port to connect. Needed for native USB port only
  }
  Ethernet.begin(mac);
  // Check for Ethernet hardware present
  if (Ethernet.hardwareStatus() == EthernetNoHardware)
  {
    Serial.println("Ethernet shield was not found.  Sorry, can't run without hardware. :(");
    while (true)
    {
      delay(1); // do nothing, no point running without Ethernet hardware
    }
  }
  if (Ethernet.linkStatus() == LinkOFF)
  {
    Serial.println("Ethernet cable is not connected.");
  }

  Serial.print("Joined LAN with IP ");
  Serial.println(Ethernet.localIP());
  Serial.flush();
  delay(2000);
}
/**
 * @brief  Function to execute the loop of the mqtt connection
 * @param  None
 * @retval None
 */
void loop()
{
  if (!mqtt_istance.connected())
  {
    reconnect();
  }

  mqtt_istance.loop();
}